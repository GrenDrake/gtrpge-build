// ////////////////////////////////////////////////////////////////////////////
// Core and general game systems
// ////////////////////////////////////////////////////////////////////////////

// ////////////////////////////////////////////////////////////////////////////
// Constant definitions
// ////////////////////////////////////////////////////////////////////////////

value pro-subject   0;
value pro-object    1;
value pro-pos       2;
value pro-adject    3;
value pro-reflex    4;

value skill-melee   0;
value skill-ranged  1;
value skill-magic   2;

value dt-blunt      "blunt";
value dt-cutting    "cutting";
value dt-piercing   "piercing";
value dt-fire       "fire";
value dt-air        "air";
value dt-earth      "earth";
value dt-water      "water";

// ////////////////////////////////////////////////////////////////////////////
// Common strings
// ////////////////////////////////////////////////////////////////////////////

value hours-per-day     1440;
value minutes-per-hour  60;
value continue-str      "Continue";

object sex-neuter
    article             "a "
    name                "neuter"
    pronouns            [ "it" "it" "its" "its" "itself" ]
;
object sex-male
    article             "a "
    name                "male"
    pronouns            [ "he" "him" "his" "his" "himself" ]
;
object sex-female
    article             "a "
    name                "female"
    pronouns            [ "she" "her" "her" "hers" "herself" ]
;

object species-human
    article             "a "
    name                "human"
;
object species-gnoll
    article             "a "
    name                "gnoll"
;
object species-dragon
    article             "a "
    name                "dragon"
;

object player
    article             "the "
    name                "player"
    sex                 sex-neuter
    species             species-human
    faction             0
    skills {
        skill-melee:    10
        skill-ranged:   12
        skill-magic:    8
    }
;

object world
    time                720 // start game at noon
    location            0
    location-name       0
    in-location         0
    new-location        0
;


// ////////////////////////////////////////////////////////////////////////////
// Utility functions
// ////////////////////////////////////////////////////////////////////////////

function add-continue() {
    asm
    continue-str $location world get-prop add-option
}

function dispatch(event extra : result) {
    asm

    run-event:
    extra 1 $body event call-method
    result store
    result typeof Object cmp done-event jnz
    result event store
    0 extra store
    run-event jmp

    done-event:

    0 $in-location world set-prop
    0 $new-location world set-prop

    $location event get-prop result store
    result 0 cmp end-location jz
    1 $in-location world set-prop
    $location world get-prop event cmp is-new-location jnz
    end-location jmp

    is-new-location:
    1 $new-location world set-prop
    event $location world set-prop
    result $location-name world set-prop
    result InfobarLeft set-info



    end-location:
//    "\nIN LOCATION: " say $in-location world get-prop say
//    "\nNEW LOCATION: " say $new-location world get-prop say
//    "\nLOCATION NAME: " say $location-name world get-prop say
    dispatch get-option
}

function main() {
    asm

    "Caged Gnoll" InfobarTitle set-info
    start 1 dispatch call
}

function print-obj(theObject) {
    asm
    $article theObject get-prop say
    $name theObject get-prop say
}

function skill-check(who skill modifier) {
    asm
    modifier None cmp-type has-mod jnz
    0 modifier store
    has-mod:
    skill $skills who get-prop get-item
    "[(" say stack-dup say "+" say modifier say ")=" say
    modifier add stack-dup say
    " vs (" say

    1 7 random stack-dup say "+" say
    1 7 random stack-dup say "+" say add
    1 7 random stack-dup say ")=" say add
    stack-dup say

    cmp success jgte
    " = fail]" say
    0 return

    success:
    1 " = success]" say
}


// ////////////////////////////////////////////////////////////////////////////
// Startup and chargen
// ////////////////////////////////////////////////////////////////////////////

object start
    body function() {
        asm
        "Welcome to game!\nYou can select options by clicking on them or (for options up to ten) by pressing the appropriate number key." say
        "Start game" chargen1 add-option
    }
;

object chargen1
    body function() {
        asm
        "What sex are you?" say
        "Male"   chargen2   sex-male    add-option-ex
        "Female" chargen2   sex-female  add-option-ex
    }
;
object chargen2
    body function(choice) {
        asm
        choice $sex player set-prop

        "What species are you?" say
        "Human"     chargen3    species-human   add-option-ex
        "Gnoll"     chargen3    species-gnoll   add-option-ex
        "Dragon"    chargen3    species-dragon  add-option-ex
    }
;
object chargen3
    body function(choice) {
        asm
        choice $species player set-prop

        "You are " say
        $sex player get-prop 1 print-obj call
        " " say
        $name $species player get-prop get-prop say
        ". Beginning the game now.\n[b]----------------------------------------[/b]\n" say
        forest-path
    }
;


// ////////////////////////////////////////////////////////////////////////////
// General item definitions
// ////////////////////////////////////////////////////////////////////////////

object gold
    article     "a "
    name        "gold piece"
    plural      "gold pieces"
    description "The coin of the realm."
;

object loincloth
    article     "a "
    name        "loincloth"
    plural      "loincloths"
    slot        "body"
;


// ////////////////////////////////////////////////////////////////////////////
// Generic nodes for resting
// ////////////////////////////////////////////////////////////////////////////

object rest-awhile
    body function() {
        asm
        "How long would you like to rest for?" say
        "One hour"      rest-awhile-doit    60  add-option-ex
        "Six hours"     rest-awhile-doit    360 add-option-ex
        "Twelve hours"  rest-awhile-doit    720 add-option-ex
        "Don't rest" $location world get-prop add-option
    }
;

object rest-awhile-doit
    body function(length) {
        asm
        "You rest for a while. " say
//        length do-rest
        0 add-continue call
    }
;


include "g_forest.inc";
include "g_town.inc";
